<?xml version="1.0" encoding="utf-8"?>
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" 
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   implements="interf.IPreview"
				   xmlns:comps="view.comps.*"
				   creationComplete="handler_creationComplete(event)">
	<fx:Metadata>
		[Event(name="previewLoadComplete", type="events.SSEvent")]
		[Event(name="transformChange", type="events.SSEvent")]
	</fx:Metadata>
	<fx:Declarations>
		<comps:TransformTool id="transf" width="100" height="100" maxDragW="{sourceWidth}" maxDragH="{sourceHeight}" visible="false" transformChange="handler_transformChange(event)" />
	</fx:Declarations>
	<s:Scroller id="viewScroller" top="0" bottom="20" width="100%">
		<s:Group id="scrollerView" width="100%" height="100%">
			<s:BitmapImage id="chessboard" fillMode="repeat" width="{viewerWidth}" height="{viewerHeight}"/>
		</s:Group>
	</s:Scroller>
	<s:HGroup width="100%" bottom="0" verticalAlign="bottom">
		<s:Label text="{sourceInfo}"/>
		<s:Spacer width="100%"/>
		<s:CheckBox id="showAllCB" change="handler_showAll(event)"/>
		<s:DropDownList id="BGDDL" change="handler_bgSelect(event)" width="140"  requireSelection="true"/>
	</s:HGroup>
	<fx:Script>
	<![CDATA[
		import events.SSEvent;
		import flash.events.Event;
		import flash.geom.Rectangle;
		import mx.collections.ArrayList;
		import mx.events.FlexEvent;
		
		import gnu.as3.gettext.FxGettext;
		
		import utils.Global;
		
		public static var LABEL_SHOW_ALL:String;
		public static var LABEL_CHESS:String;
		public static var LABEL_WHITE:String;
		public static var LABEL_BLACK:String;
		
		private var _maxSizeLimit:String = "source";
		
		public function get maxSizeLimit():String 
		{
			return _maxSizeLimit;
		}
		
		public function set maxSizeLimit(value:String):void 
		{
			_maxSizeLimit = value;
			updateTransformBarMax();
		}
		
		
		[Bindable] public var controlBar:Boolean;
		
		[Bindable] public var scaleContent:Boolean;
		/**
		 * 被载入的显示对象的实际宽度。例如，对于swf来说，就是swf文件的宽度。
		 */
		private var _sourceWidth:int = 100;
		[Bindable] 
		public function get sourceWidth():int 
		{
			return _sourceWidth;
		}
		
		public function set sourceWidth(value:int):void 
		{
			_sourceWidth = value;
			updateTransformBarMax();
		}
		/**
		 * 被载入的显示对象的实际高度。
		 */
		private var _sourceHeight:int = 100;
		[Bindable] 
		public function get sourceHeight():int 
		{
			return _sourceHeight;
		}
		
		public function set sourceHeight(value:int):void 
		{
			_sourceHeight = value;
			updateTransformBarMax();
		}
		
		/**
		 * viewer的显示宽度。当scaleContent为false的时候，它与sourceWidth相等。
		 */
		private var _viewerWidth:int=100;
		[Bindable] 
		public function get viewerWidth():int 
		{
			return _viewerWidth;
		}
		
		public function set viewerWidth(value:int):void 
		{
			_viewerWidth = value;
			updateTransformBarMax();
		}
		/**
		 * viewer的显示高度。当scaleContent为false的时候，它与sourceWidth相等。
		 */
		private var _viewerHeight:int=100;
		[Bindable] 
		public function get viewerHeight():int 
		{
			return _viewerHeight;
		}
		
		public function set viewerHeight(value:int):void 
		{
			_viewerHeight = value;
			updateTransformBarMax();
		}
		
		[Bindable] public var sourceInfo:String;
		
		protected function handler_creationComplete($evt:FlexEvent):void
		{
			LABEL_SHOW_ALL = FxGettext.gettext("Show all");
			LABEL_CHESS = FxGettext.gettext("Chess background");
			LABEL_WHITE = FxGettext.gettext("White background");
			LABEL_BLACK = FxGettext.gettext("Black background");
			init();
		}
		
		protected function init():void
		{
			chessboard.source = Global.BMP_CHESS;
			showAllCB.label = LABEL_SHOW_ALL;
			var __bgAL:ArrayList = new ArrayList([LABEL_CHESS, LABEL_BLACK,LABEL_WHITE]);
			BGDDL.dataProvider = __bgAL;
		}
		
		private var _transformControlBar:TransformToolControlBar;
		[Bindable]
		public function get transformControlBar():TransformToolControlBar 
		{
			return _transformControlBar;
		}
		
		private function updateTransformBarMax():void
		{
			if(transformControlBar)
			{
				var __limitW:int = this[maxSizeLimit + "Width"];
				var __limitH:int = this[maxSizeLimit + "Height"];
				transformControlBar.maxX =  __limitW - 1;
				transformControlBar.maxY = __limitH - 1;
				transformControlBar.maxW = __limitW;
				transformControlBar.maxH = __limitH;
			}
		}
		
		public function set transformControlBar(value:TransformToolControlBar):void 
		{
			_transformControlBar = value;
			updateTransformBarMax();
			if(transformControlBar)
			{
				transformControlBar.useCustomSizeCB.addEventListener(Event.CHANGE, handler_useCustomSizeChange);
				transformControlBar.addEventListener(SSEvent.TRANSFORM_FRAME_CHANGE, handler_transformFrameChange);
				updateTransformVisible();
			}
		}
		
		/**
		 * @private
		 * 更新变形对象的尺寸
		 */
		private function updateTransformVisible():void
		{
			transf.visible = (transformControlBar?transformControlBar.useCustomSizeCB.selected : false) && !scaleContent;
			if(transf.visible)
			{
				updateTransformControl();
			}
		}
		
		/**
		 * @private
		 * 更新显示对象的显示
		 */
		private function updateTransformControl():void
		{
			if(transformControlBar)
			{
				transformControlBar.transformRect = transf.transformRect;
			}
		}
		
		private function handler_useCustomSizeChange($evt:Event):void 
		{
			updateTransformVisible();
		}
		
		protected function get bg():*
		{
			return null;
		}
		
		private function handler_showAll($evt:Event):void
		{
			scaleContent = showAllCB.selected;
			handler_loadComplete(null);
		}
		
		private function handler_bgSelect($evt:Event):void
		{
			var __bgName:String = BGDDL.selectedItem as String;
			var __source:* = null;
			if(__bgName == LABEL_CHESS)
				__source = Global.BMP_CHESS;
			else if(__bgName == LABEL_WHITE)
				__source = new BitmapData(5,5,false,0xFFFFFF);
			else
				__source = new BitmapData(5,5,false,0x000000);
			bg.source = __source;
		}
		
		protected function handler_loadComplete($event:Event):void
		{
		}
		
		protected function handler_transformFrameChange($evt:SSEvent):void
		{
			transf.transformRect = transformControlBar.transformRect;
		}
		
		protected function handler_transformChange($evt:SSEvent):void
		{
			updateTransformControl();
			this.dispatchEvent($evt);
		}
		
		public function destroy():void
		{
		}
		
		public function set source($so:*):void
		{
		}
		
		public function get content():IBitmapDrawable
		{
			return null;
		}
		
	]]>
	</fx:Script>
</s:BorderContainer>
