<?xml version="1.0" encoding="utf-8"?>
<!-- zengrong.net -->
<!-- 创建者:zrong(zrongzrong@gmail.com) -->
<!-- 创建时间：2011-8-5 -->
<!-- 显示载入的SWF动画或者图像 -->
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				   xmlns:s="library://ns.adobe.com/flex/spark" 
				   xmlns:comps="comps.*" 
				   implements="interf.IPreview"
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   creationComplete="init()">
	<fx:Metadata>
		[Event(name="previewLoadComplete", type="events.InfoEvent")]
	</fx:Metadata>
	<s:Scroller id="viewScroller" top="0" bottom="20" width="100%">
		<s:viewport>
			<s:Group id="scrollerView" width="100%" height="100%">
				<s:BitmapImage id="chessboard" source="@Embed('checks.png')" fillMode="repeat" width="{viewerWidth}" height="{viewerHeight}"/>
				<s:SWFLoader id="viewer" scaleContent="{scaleContent}" showBusyCursor="true" 
							 complete="handler_swfcomplete(event)" />
				<comps:TransformTool id="transf" width="100" height="100" maxW="{sourceWidth}" maxH="{sourceHeight}" 
									 visible="{showTransformTool&amp;&amp;!scaleContent}"/>
			</s:Group>
		</s:viewport>
	</s:Scroller>
	<s:Label right="0" bottom="0" text="W:{sourceWidth},H:{sourceHeight}"/>
	<fx:Script>
	<![CDATA[
		import events.InfoEvent;
		
		[Bindable] public var showTransformTool:Boolean;
		[Bindable] public var scaleContent:Boolean;
		
		/**
		 * 被载入的显示对象的实际宽度。例如，对于swf来说，就是swf文件的宽度。
		 */
		[Bindable] public var sourceWidth:int=100;
		/**
		 * 被载入的显示对象的实际高度。
		 */
		[Bindable] public var sourceHeight:int=100;
		
		/**
		 * viewer的显示宽度。当scaleContent为false的时候，它与sourceWidth相等。
		 */
		[Bindable] public var viewerWidth:int=100;
		/**
		 * viewer的显示高度。当scaleContent为false的时候，它与sourceWidth相等。
		 */
		[Bindable] public var viewerHeight:int=100;
		
		private function init():void
		{
			var __menu:ContextMenu = new ContextMenu();
			__menu.addEventListener(Event.SELECT, handler_menuSelect);
			__menu.customItems = [new ContextMenuItem('显示全部')];
			this.contextMenu = __menu;
		}
		
		//----------------------------------------
		// public
		//----------------------------------------
		public function destroy():void
		{
			viewer.unloadAndStop(true);
//			sourceWidth = 0;
//			sourceHeight = 0;
//			viewerWidth = 0;
//			viewerHeight = 0;
		}
		
		//----------------------------------------
		// getter/setter
		//----------------------------------------
		public function set source($so:*):void
		{
			viewer.unloadAndStop(true);
			viewer.load($so);
		}
		
		public function get content():IBitmapDrawable
		{
			return viewer.content;
		}
		
		//----------------------------------------
		// handler
		//----------------------------------------
		
		private function handler_menuSelect($evt:Event):void
		{
			var __menuItem:ContextMenuItem = $evt.target as ContextMenuItem;
			__menuItem.checked = !__menuItem.checked;
			scaleContent = __menuItem.checked;
			handler_swfcomplete(null);
		}
		
		protected function handler_viewerSecurityError($event:SecurityErrorEvent):void
		{
			trace('载入安全错误：', $event.text);
		}
		
		protected function handler_swfcomplete($event:Event):void
		{
			trace('ImagePreview载入成功:', viewer.source);
			//重设viewer和transf
			sourceWidth = viewer.content.loaderInfo.width;
			sourceHeight = viewer.content.loaderInfo.height;
			if(scaleContent)
			{
				viewer.width = viewScroller.viewport.width;
				viewer.height = viewScroller.viewport.height;
				viewerWidth = viewScroller.viewport.width;
				viewerHeight = viewScroller.viewport.height;
			}
			else
			{
				viewer.width = sourceWidth;
				viewer.height = sourceHeight;
				viewerWidth = sourceWidth;
				viewerHeight = sourceHeight;
			}
			this.dispatchEvent(new InfoEvent(InfoEvent.PREVIEW_LOAD_COMPLETE));
			trace(viewer.width, viewer.height, viewer.contentWidth, viewer.contentHeight, sourceWidth, sourceHeight);
		}
	]]>
	</fx:Script>
</s:BorderContainer>
